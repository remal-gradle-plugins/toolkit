import org.gradle.util.GradleVersion

project.ext['javaGradlePluginWithoutPublish'] = true

apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'name.remal.generate-sources'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

afterEvaluate {
    GradleVersion versionToTest = GradleVersion.version(property('gradle-api.version'))
    if (versionToTest < GradleVersion.current()) {
        tasks.matching { it instanceof AbstractTestTask }.all { disableTask(it) }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

dependencies {
    indirectApi('org.eclipse.jgit:org.eclipse.jgit:6.3.0.202209071007-r') { transitive = false }

    relocateClasses project(':toolkit')

    testImplementation project(':testkit')
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

configurations.all { attributes.attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category.class, Category.LIBRARY)) }

rootProject.buildscript.configurations.classpathApi.allDependencies.forEach { Dependency dep ->
    project.configurations.api.dependencies.add(dep.copy())
}

configurations.gradlePluginApiDependencies {
    exclude(group: 'com.google.code.findbugs')
    exclude(group: 'com.google.errorprone')
    exclude(group: 'com.google.guava')
    exclude(group: 'com.google.code.gson')
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Map<File, String> scriptsToInclude = [:]
rootProject.ext['includedBuildScripts'].forEach { String includedBuildScript ->
    File includedBuildScriptFile = rootProject.file(includedBuildScript)
    String includedBuildScriptResource = includedBuildScript.replaceFirst(/^gradle\//, '')
    scriptsToInclude[includedBuildScriptFile] = "name/remal/gradleplugins/toolkit/buildlogic/${includedBuildScriptResource}"
}

tasks.named('generateResources') {
    inputs.files(configurations.compileClasspath).optional().withPropertyName('compileClasspath')
    scriptsToInclude.forEach { includedBuildScriptFile, resource ->
        inputs.file(includedBuildScriptFile)
        binaryFile(resource) { OutputStream outputStream ->
            String content = includedBuildScriptFile.getText('UTF-8')

            content = content.replaceAll(/\$\{\s*(rootBuildscriptClasspathVersionOf)\s*\(\s*('|")(.+?)\2\s*\)\s*\}/) { matcher ->
                project.property(matcher[1])(matcher[3])
            }

            outputStream.write(content.getBytes('UTF-8'))
        }
    }
}

tasks.named('generateJava') {
    inputs.property('project.version', project.provider { project.version })
    classFile('name.remal.gradleplugins.toolkit.buildlogic', 'BuildLogicPlugin') {
        it.writePackage()
        it.println("")
        it.writeStaticImport("name.remal.gradleplugins.toolkit.ResourceUtils", "getResourceUrl")
        it.println("")
        it.writeImport("org.gradle.api.Plugin")
        it.writeImport("org.gradle.api.Project")
        it.println("")
        it.writeSuppressWarnings("checkstyle:LineLength", "checkstyle:RightCurlyAlone", "InjectedReferences")
        it.writeBlock("public class ${it.simpleName} implements Plugin<Project>") {
            it.writeBlock("public void apply(Project project)") {
                scriptsToInclude.values().forEach { resource ->
                    it.writeln("project.apply(it -> it.from(getResourceUrl(\"/${it.escapeJava(resource)}\", ${it.simpleName}.class)));")
                }

                it.println("")

                it.println("project.allprojects(proj -> ")
                it.println("    proj.getConfigurations()")
                it.println("        .getByName(\"projectDependencyConstraints\")")
                it.println("        .getDependencies()")
                it.println("        .add(")
                it.println("            proj.getDependencies().platform(\"name.remal.gradle-plugins.toolkit:toolkit-bom:${project.version}\")")
                it.println("        )")
                it.println(");")
            }
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

tasks.named('generateFunctionalJava') {
    classFile('name.remal.gradleplugins.toolkit.buildlogic', 'BuildLogicPluginSimpleFunctionalTest') {
        it.writePackage()
        it.println("")
        it.writeImport("lombok.RequiredArgsConstructor")
        it.writeImport("name.remal.gradleplugins.toolkit.testkit.functional.GradleProject")
        it.writeImport("org.junit.jupiter.api.Test")
        it.println("")
        it.println("@RequiredArgsConstructor")
        it.writeSuppressWarnings("checkstyle:LineLength", "checkstyle:RightCurlyAlone")
        it.writeBlock("class ${it.simpleName}") {
            it.println("")
            it.println("private final GradleProject project;")

            it.println("")
            it.println("@Test")
            it.writeBlock("void applyPlugin()") {
                it.println("project.forBuildFile(build -> {")
                it.println("    build.applyPlugin(\"name.remal.toolkit.build-logic\");")
                it.println("    build.registerDefaultTask(\"allClasses\");")
                it.println("});")
                it.println("project.assertBuildSuccessfully();")
            }
        }
    }
}

import org.gradle.util.GradleVersion

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'name.remal.generate-sources'

project.fatJarWith(project(':toolkit-annotations'))

; [
    'build-generate-checkstyle.xsl.gradle',
    'build-generate-CorePluginConfigurationCacheSupport.gradle',
    'build-generate-GradleCompatibilityJava.gradle',
    'build-generate-MembersFinder.gradle',
    'build-generate-SneakyThrowUtils.gradle',
].forEach { fileName ->
    apply from: fileName
}

dependencies {
    classesRelocation 'io.github.classgraph:classgraph:4.8.184'
    classesRelocation 'org.ec4j.core:ec4j-core:1.2.0'
    classesRelocation 'net.htmlparser.jericho:jericho-html:3.4'
    classesRelocation('org.eclipse.jgit:org.eclipse.jgit:6.10.1.202505221210-r') {
        exclude group: 'org.slf4j'
    }

    optional platform('org.immutables:bom:2.12.0')
    optional 'org.immutables:gson'


    testImplementation project(':testkit')
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Configuration pluginsCompatibilityToTestConf = configurations.create('pluginsCompatibilityToTest') {
    configurations.testImplementation.extendsFrom(it)
}

Set<String> usedPluginCompatibilityVersionProperties = Collections.synchronizedSet(new LinkedHashSet<>())

pluginsCompatibilityToTestConf.dependencies.addAll(
    [
        'org.jetbrains.kotlin.jvm:org.jetbrains.kotlin.jvm.gradle.plugin:2.2.21',
        'io.spring.dependency-management:io.spring.dependency-management.gradle.plugin:1.1.7',
        'com.softeq.gradle.itest:com.softeq.gradle.itest.gradle.plugin:1.0.4',
        'org.unbroken-dome.test-sets:org.unbroken-dome.test-sets.gradle.plugin:4.1.0',
    ].collect {
        project.dependencies.create(it) { ExternalModuleDependency dep ->
            String versionProperty = "${dep.group}.version".toString()
            usedPluginCompatibilityVersionProperties.add(versionProperty)
            dep.version {
                String overriddenVersion = project.findProperty(versionProperty)?.toString()
                if (overriddenVersion != null) {
                    strictly(overriddenVersion)
                }
            }
        }
    }
)

rootProject.allprojects {
    tasks.withType(Test).configureEach {
        if (project.isRunningOnCi) {
            String kotlinPluginTag = 'kotlin-plugin'

            GradleVersion latestStableGradleVersion = project.getStableGradleVersions()[0]
            boolean isGradleLatestStable = latestStableGradleVersion == project.gradleApiVersion

            boolean hasKotlinPluginCompatibilityVersionProperty = usedPluginCompatibilityVersionProperties
                .findAll { "$it.".startsWith('org.jetbrains.kotlin.') && it.endsWith('.version') }
                .any { project.findProperty(it) != null }

            useJUnitPlatform {
                if (isGradleLatestStable) {
                    // do nothing - test Kotlin plugin integration logic with all other tests
                } else if (hasKotlinPluginCompatibilityVersionProperty) {
                    includeTags.add(kotlinPluginTag)
                } else {
                    excludeTags.add(kotlinPluginTag)
                }
            }
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Configuration saxonConf = configurations.create('saxon') {
    dependencies.add(project.dependencies.create('net.sf.saxon:Saxon-HE:12.9'))
}

TaskProvider testXsltWithSaxon = tasks.register('testXsltWithSaxon', Test) {
    TaskProvider<Test> testProvider = tasks.named('test', Test)
    dependsOn(testProvider)

    classpath = saxonConf + files(testProvider.map { it.classpath })
    testClassesDirs = files(testProvider.map { it.testClassesDirs })

    onlyIf {
        Test test = testProvider.get()
        includes = test.includes
        excludes = test.excludes
        environment = test.environment
        systemProperties = test.systemProperties
        return true
    }

    useJUnitPlatform {
        includeTags = ['xslt']
    }
}

tasks.named('check').configure { dependsOn(testXsltWithSaxon) }
tasks.matching { it.name == 'allTests' }.configureEach { dependsOn(testXsltWithSaxon) }

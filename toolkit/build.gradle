apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'name.remal.generate-sources'

; [
    'build-generate-checkstyle.xsl.gradle',
    'build-generate-CorePluginConfigurationCacheSupport.gradle',
    'build-generate-GradleCompatibilityJava.gradle',
    'build-generate-MembersFinder.gradle',
    'build-generate-SneakyThrowUtils.gradle',
].forEach { fileName ->
    apply from: fileName
}

dependencies {
    classesRelocation 'io.github.classgraph:classgraph:4.8.181'
    classesRelocation 'org.ec4j.core:ec4j-core:1.1.1'
    classesRelocation 'net.htmlparser.jericho:jericho-html:3.4'
    classesRelocation('org.eclipse.jgit:org.eclipse.jgit:6.10.1.202505221210-r') {
        exclude group: 'org.slf4j'
    }

    optional platform('org.immutables:bom:2.11.3')
    optional 'org.immutables:gson'


    testImplementation project(':testkit')


    components {
        withModule('org.unbroken-dome.gradle-plugins:gradle-testsets-plugin', TargetJvmVersionRule) { params(8) }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Configuration pluginsCompatibilityToTestConf = configurations.create('pluginsCompatibilityToTest') {
    configurations.testImplementation.extendsFrom(it)
}

Set<String> usedPluginCompatibilityVersionProperties = new LinkedHashSet<>()

pluginsCompatibilityToTestConf.dependencies.addAll(
    [
        'org.jetbrains.kotlin.jvm:org.jetbrains.kotlin.jvm.gradle.plugin:2.2.10',
        'io.spring.dependency-management:io.spring.dependency-management.gradle.plugin:1.1.7',
        'com.softeq.gradle.itest:com.softeq.gradle.itest.gradle.plugin:1.0.4',
        'org.unbroken-dome.test-sets:org.unbroken-dome.test-sets.gradle.plugin:4.1.0',
    ].collect {
        project.dependencies.create(it) { ExternalModuleDependency dep ->
            dep.version {
                String versionProperty = "${dep.group}.version".toString()
                usedPluginCompatibilityVersionProperties.add(versionProperty)
                String overriddenVersion = project.findProperty(versionProperty)?.toString()
                if (overriddenVersion != null) {
                    strictly(overriddenVersion)
                }
            }
        }
    }
)


/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

project.fatJarWith(project(':toolkit-annotations'))

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

tasks.withType(Test).configureEach {
    if (project.isRunningOnCi) {
        boolean hasKotlinPluginCompatibilityVersionProperty = usedPluginCompatibilityVersionProperties
            .findAll { "$it.".startsWith('org.jetbrains.kotlin.') }
            .any { project.findProperty(it) != null }
        if (!hasKotlinPluginCompatibilityVersionProperty) {
            useJUnitPlatform {
                excludeTags = ['kotlin-plugin']
            }
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Configuration saxonConf = configurations.create('saxon') {
    dependencies.add(project.dependencies.create('net.sf.saxon:Saxon-HE:12.8'))
}

TaskProvider testXsltWithSaxon = tasks.register('testXsltWithSaxon', Test) {
    TaskProvider<Test> testProvider = tasks.named('test', Test)
    dependsOn(testProvider)

    classpath = saxonConf + files(testProvider.map { it.classpath })
    testClassesDirs = files(testProvider.map { it.testClassesDirs })

    onlyIf {
        Test test = testProvider.get()
        includes = test.includes
        excludes = test.excludes
        environment = test.environment
        systemProperties = test.systemProperties
        return true
    }

    useJUnitPlatform {
        includeTags = ['xslt']
    }
}

tasks.named('check').configure { dependsOn(testXsltWithSaxon) }
tasks.matching { it.name == 'allTests' }.configureEach { dependsOn(testXsltWithSaxon) }

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

@CacheableRule
abstract class TargetJvmVersionRule implements ComponentMetadataRule {

    final Integer jvmVersion

    @Inject
    TargetJvmVersionRule(Integer jvmVersion) {
        this.jvmVersion = jvmVersion
    }

    void execute(ComponentMetadataContext context) {
        context.details.allVariants {
            attributes {
                attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, jvmVersion)
            }
        }
    }

    @Override
    String toString() {
        return "${getClass().simpleName}[$jvmVersion]"
    }

}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'name.remal.classes-relocation'
apply plugin: 'name.remal.generate-sources'
apply from: 'build-generate-MembersFinder.gradle'
apply from: 'build-generate-SneakyThrowUtils.gradle'
apply from: 'build-generate-checkstyle.xsl.gradle'

dependencies {
    relocateClasses 'org.jdom:jdom2'
    relocateClasses 'io.github.classgraph:classgraph:4.8.153'
    relocateClasses 'org.ec4j.core:ec4j-core:0.3.0'
    relocateClasses 'net.htmlparser.jericho:jericho-html:3.4'
    relocateClasses('org.eclipse.jgit:org.eclipse.jgit:6.4.0.202211300538-r') {
        exclude group: 'org.slf4j'
    }


    testImplementation project(':testkit')
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Project annotationsProject = rootProject.findProject(':toolkit-annotations')
project.fatJarWith(annotationsProject)

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

tasks.named('generateJava') { Task it ->
    inputs.property('project.group', project.group)
    inputs.property('project.name', project.name)
    inputs.property('project.version', project.version)
    classFile('name.remal.gradleplugins.toolkit', 'ToolkitBuildInfo') {
        it.writePackage()
        it.println("")
        it.writeStaticImport("lombok.AccessLevel", "PRIVATE")
        it.println("")
        it.writeImport("lombok.NoArgsConstructor")
        it.println("")
        it.println("@NoArgsConstructor(access = PRIVATE)")
        it.writeBlock("abstract class ToolkitBuildInfo") {
            it.println("public static final String TOOLKIT_GROUP_ID = \"${it.escapeJava(project.group)}\";")
            it.println("public static final String TOOLKIT_ARTIFACT_IT = \"${it.escapeJava(project.name)}\";")
            it.println("public static final String TOOLKIT_VERSION = \"${it.escapeJava(project.version)}\";")
        }
    }
}

if (project.isBuildSrcProject) return

allprojects {
    pluginManager.withPlugin('name.remal.classes-relocation') {
        dependencies {
            excludeFromForcedClassesRelocation 'com.google.code.findbugs:jsr305'
            excludeFromForcedClassesRelocation 'org.jetbrains:annotations'
        }

        classesRelocation {
            relocatedClassesPackageName = "${project.calculateBaseJavaPackage()}.internal._relocated"
        }
        tasks.withType(AbstractCompile).configureEach {
            it.inputs.property('relocatedClassesPackageName', classesRelocation.relocatedClassesPackageName)
        }

        configurations.matching { ['relocateClasses', 'excludeFromClassesRelocation', 'excludeFromForcedClassesRelocation'].contains(it.name) }.all { Configuration conf ->
            conf.canBeConsumed = false

            conf.attributes {
                attribute(
                    Usage.USAGE_ATTRIBUTE,
                    objects.named(Usage.class, Usage.JAVA_RUNTIME)
                )
                attribute(
                    Category.CATEGORY_ATTRIBUTE,
                    objects.named(Category.class, Category.LIBRARY)
                )
            }
        }
    }
}

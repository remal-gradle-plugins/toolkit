import org.gradle.util.GradleVersion

allprojects {
    File ciPropertiesFile = rootProject.file("${rootProject.buildDir}/gradle-ci.properties")
    synchronized (rootProject) {
        if (!rootProject.hasProperty('ciProperties')) {
            Properties props = new Properties()
            rootProject.ext['ciProperties'] = props
            if (ciPropertiesFile.isFile()) {
                ciPropertiesFile.newInputStream().withCloseable { props.load(it) }
            }
        }
    }

    def getCiProperty = { String name, String defaultValue, boolean storeValue = false ->
        Properties ciProperties = rootProject.ext['ciProperties']
        String value = ciProperties.getProperty(name)
        if (value == null) {
            value = rootProject.findProperty(name)?.toString()
        }
        if (value == null) {
            value = System.getProperty(name)
        }
        if (value == null) {
            String envName = name.replaceAll(/[^\w]/, '_').toUpperCase()
            value = System.getenv(envName)
        }

        if (value != null) {
            if (storeValue) {
                ciProperties.setProperty(name, value)
                synchronized (rootProject) {
                    ciPropertiesFile.parentFile.mkdirs()
                    ciPropertiesFile.newOutputStream().withCloseable { ciProperties.store(it, null) }
                }
            }
            return value
        } else {
            return defaultValue
        }
    }

    ext['git-ref'] = getCiProperty('git-ref', '')

    ext['gradle-api.version'] = getCiProperty('gradle-api.version', GradleVersion.current().version)

    ext['disable-compilation'] = getCiProperty('disable-compilation', 'false')
    ext['disable-tests'] = getCiProperty('disable-tests', 'false')
    ext['disable-verification'] = getCiProperty('disable-verification', 'false')
    ext['disable-verification-except-tests'] = getCiProperty('disable-verification-except-tests', 'false')

    ext['repository-name'] = getCiProperty('repository-name', '', true)
    ext['repository-full-name'] = getCiProperty('repository-full-name', '', true)
    ext['repository-html-url'] = getCiProperty('repository-html-url', '', true)
    ext['repository-description'] = getCiProperty('repository-description', '', true)
    ext['repository-topics'] = getCiProperty('repository-topics', '', true)
    ext['repository-is-template'] = getCiProperty('repository-is-template', '', true)
    ext['repository-owner-name'] = getCiProperty('repository-owner-name', '', true)
    ext['repository-license-name'] = getCiProperty('repository-license-name', '', true)
    ext['repository-license-html-url'] = getCiProperty('repository-license-html-url', '', true)


    File rootProjectDir = file(rootDir)
    if (rootProjectDir.name == 'buildSrc') rootProjectDir = rootProjectDir.parentFile
    ext['rootProjectDir'] = rootProjectDir

    String currentVersion = project.version.toString()
    if (currentVersion.matches(/^(0|[1-9]\d*)(\D.*)?$/)) {
        ext['majorVersion'] = currentVersion.replaceFirst(/^(\d+).*/, '$1')
    }

    String rootPackage = rootProject.group
    if (!rootPackage.endsWith(".${rootProject.name}")) {
        rootPackage += ".${rootProject.name}"
    }
    ext['baseJavaPackage'] = "${rootPackage}${project.path}"
        .replace('-', '')
        .replace(':', '.')
        .replaceFirst(/\.+$/, '')
        .replaceAll(/\.{2,}/, '.')
}

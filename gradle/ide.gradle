import groovy.json.JsonBuilder
import org.gradle.plugins.ide.idea.model.IdeaLanguageLevel

apply plugin: 'idea'
apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'
apply plugin: 'name.remal.idea-settings'

allprojects {
    apply plugin: 'idea'
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

idea.project {
    project.findProperty('java-build.version')?.toString()?.with {
        targetBytecodeVersion = JavaVersion.toVersion(it)
        languageLevel = new IdeaLanguageLevel(it.toInteger())
    }

    settings {
        doNotDetectFrameworks(
            'AngularCLI',
            'android',
            'hibernate',
            'javaeeApplication',
            'jpa',
            'Spring',
            'web',
        )
    }
}

ideaSettings {
    nullability {
        defaultNotNullAnnotation = 'org.jspecify.annotations.NonNull'
        defaultNullableAnnotation = 'org.jspecify.annotations.Nullable'
    }
    runOnSave {
        reformatMode = 'WHOLE_FILE'
        optimizeImports = true
    }
}

TaskProvider processIdeaSettings = tasks.named('processIdeaSettings') {
    if (project.isRunningOnCi) {
        doFirst {
            String jsonString = new JsonBuilder([ideaDirPath: rootProject.file('.idea').absolutePath]).toPrettyString()
            rootProject.file('layout.json').setText(jsonString, 'UTF-8')
        }
    }
}

tasks.named('pushBackTasks') { dependsOn(processIdeaSettings) }

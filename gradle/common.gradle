import static java.util.concurrent.TimeUnit.HOURS
import static org.gradle.language.base.plugins.LifecycleBasePlugin.VERIFICATION_GROUP

import java.time.Duration

allprojects {
    apply plugin: 'name.remal.merge-resources'

    configurations.configureEach { Configuration conf ->
        conf.resolutionStrategy.cacheDynamicVersionsFor(1, HOURS)
    }

    tasks.configureEach { timeout = Duration.ofMinutes(30) }

    tasks.withType(AbstractCopyTask).configureEach { AbstractCopyTask task ->
        task.duplicatesStrategy = DuplicatesStrategy.FAIL
    }

    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    tasks.matching { project.isVerificationTask(it) }.configureEach { group = VERIFICATION_GROUP }

    tasks.matching { it instanceof ProcessForkOptions }.configureEach { ProcessForkOptions task ->
        task.workingDir = project.projectDir
    }
}

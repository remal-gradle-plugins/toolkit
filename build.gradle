/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    configurations.create('classpathPlugins') { configurations.classpath.extendsFrom(it) }
    configurations.create('classpathLibs') { configurations.classpath.extendsFrom(it) }
    dependencies {
        classpathPlugins 'name.remal.gradle-plugins.test-source-sets:test-source-sets:2.2.3'
        classpathPlugins 'com.gradle.publish:plugin-publish-plugin:1.0.0'
        classpathPlugins 'name.remal.gradle-plugins.idea-settings:idea-settings:2.0.0'
        classpathPlugins 'gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1.6'
        classpathPlugins 'name.remal:gradle-plugins:1.7.0'
        classpathPlugins 'io.github.gradle-nexus:publish-plugin:1.1.0'
        classpathPlugins 'net.ltgt.gradle:gradle-errorprone-plugin:2.0.2'

        classpathLibs('com.google.guava:guava:31.1-jre') {
            exclude(group: 'com.google.code.findbugs')
            exclude(group: 'org.checkerframework')
            exclude(group: 'com.google.errorprone')
            exclude(group: 'com.google.j2objc')
        }
    }
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    group = 'name.remal.gradle-plugins.toolkit'
    version = '0-SNAPSHOT'
}

List<String> includedBuildScripts = [
    'gradle/properties.gradle',
    'gradle/ci.gradle',
    'gradle/dependencies.gradle',
    'gradle/java.gradle',
    'gradle/classes-relocation.gradle',
    'gradle/groovy.gradle',
    'gradle/checkstyle.gradle',
    'gradle/errorprone.gradle',
    'gradle/gradle-plugin.gradle',
    'gradle/base-package.gradle',
    'gradle/publish-gradle-plugin.gradle',
    'gradle/publish-maven.gradle',
    'gradle/publish-maven-bom.gradle',
    'gradle/publish-maven-central.gradle',
    'gradle/signing.gradle',
    'gradle/process-readme.gradle',
    'gradle/ide.gradle',
].toUnique()
project.ext['includedBuildScripts'] = includedBuildScripts
includedBuildScripts.forEach { apply from: it }

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    pluginManager.withPlugin('java') {
        dependencies {
            optional 'name.remal.gradle-api:gradle-api'

            annotationProcessorAll 'com.google.auto.service:auto-service'
            compileOnlyAll 'com.google.auto.service:auto-service-annotations'
            annotationProcessorAll 'org.immutables:value'
            compileOnlyAll 'org.immutables:value-annotations'
            compileOnlyAll 'org.immutables:builder'

            relocateClasses 'org.apache.commons:commons-lang3'
            relocateClasses 'org.apache.commons:commons-text'
            relocateClasses 'com.google.guava:guava'
            relocateClasses 'org.ow2.asm:asm'


            testImplementation 'org.apache.commons:commons-lang3'
            testImplementation 'com.google.guava:guava'
            testImplementation 'org.ow2.asm:asm'
            testImplementation 'org.ow2.asm:asm-tree'
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'name.remal.classes-relocation'
apply plugin: 'name.remal.generate-sources'
apply from: 'build-include-classes.gradle'
apply from: 'build-generate-MembersFinder.gradle'
apply from: 'build-generate-SneakyThrowUtils.gradle'
apply from: 'build-generate-checkstyle.xsl.gradle'

dependencies {
    optional 'org.ow2.asm:asm'
    optional 'com.puppycrawl.tools:checkstyle'

    relocateClasses 'io.github.classgraph:classgraph'
    relocateClasses 'org.ec4j.core:ec4j-core'
    relocateClasses 'org.jdom:jdom'
    relocateClasses 'net.htmlparser.jericho:jericho-html'
    relocateClasses('org.eclipse.jgit:org.eclipse.jgit') {
        exclude group: 'org.slf4j'
    }

    compileOnly project(':toolkit--java-common')
    includeClasses project(':toolkit--java-common')
    includeClasses project(':toolkit--java-9')


    testImplementation project(':testkit')
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

tasks.named('generateJava') {
    classFile('name.remal.gradleplugins.toolkit', 'ToolkitBuildInfo') {
        it.writePackage()
        it.println("")
        it.writeStaticImport("lombok.AccessLevel", "PRIVATE")
        it.println("")
        it.writeImport("lombok.NoArgsConstructor")
        it.println("")
        it.println("@NoArgsConstructor(access = PRIVATE)")
        it.writeBlock("abstract class ToolkitBuildInfo") {
            it.println("public static final String TOOLKIT_GROUP_ID = \"${it.escapeJava(project.group)}\";")
            it.println("public static final String TOOLKIT_ARTIFACT_IT = \"${it.escapeJava(project.name)}\";")
            it.println("public static final String TOOLKIT_VERSION = \"${it.escapeJava(project.version)}\";")
        }
    }
}
